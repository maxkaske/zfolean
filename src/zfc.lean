
import fol
import data.set

/-!
# ZFC set theory

In this file we define the signature and axioms of Zermeloâ€“Fraenkel with axiom of choice.
and give a natural deduction proof of the induction principle in its set theoretical form.

We also briefly discuss how statements about named variables like 
  "Ï† is a formula with B not free"
in the formulation of the axiom schemes of separation and replacement
can be translated to De Bruijn variables using lifts.

## Main results

- `omega_smallest_inductive_provable_within_zfc`: 
    we show that ZFC proves that Ï‰ is the smallest inductive set. a direct consequence of
- `omega_smallest_inductive`: 
    a natural deduction proof that Ï‰ is the smallest inductive set

## References

* [H.D. Ebbinghaus, *EinfÃ¼hrung in die Mengenlehre*] [EBB03] 
  -- for its classical development of ZFC
-/

namespace zfc
open fol

universe variable u

-- for inserting elements into a data.set
local infix ` >> ` := insert

section zfc_language

/- We will use single predicate for membership and nothing else -/
inductive pred_symb : â„• â†’ Type u
| elem : pred_symb 2
inductive func_symb : â„• â†’ Type u

def ğ’® : signature := { func_symb:= zfc.func_symb , pred_symb:= zfc.pred_symb }

-- Definition and notation for our predicate
@[simp] def memb (tâ‚ tâ‚‚: term ğ’®): formula ğ’® := papp (papp (pred pred_symb.elem) tâ‚) tâ‚‚
infix ` âˆˆ' `:100 := memb

-- Predicates in our meta signature
def subset (X Y : term ğ’®) : (formula ğ’®) := âˆ€' ((#0 âˆˆ' (X â†‘ 1 ï¼   0)) â†’' (#0 âˆˆ' (Y â†‘ 1 ï¼   0)))
infix ` 'âŠ† `:100 := subset

def is_successor_of( X Y: term ğ’® ) : formula ğ’® 
  := âˆ€'( (#0 âˆˆ' (X â†‘ 1 ï¼  0 )) â†”' ((#0 âˆˆ' (Y â†‘ 1 ï¼   0)  âˆ¨' (#0 =' (Y â†‘ 1 ï¼   0) ))))
infix ` is_successor_of' `:100 := is_successor_of

def is_empty (x : term ğ’®) : formula ğ’® := âˆ€' ( (#0 âˆˆ' (x â†‘ 1 ï¼  0) ) â†”' Â¬'(#0 =' #0) )
postfix ` is_empty'`:100 := is_empty 

def is_inductive (x : term ğ’®) : formula ğ’® := (âˆ€' (#0 is_empty' â†’' (#0 âˆˆ' (x â†‘ 1 ï¼  0))))      
  âˆ§' ( âˆ€'(#0 âˆˆ' (x â†‘ 1 ï¼   0) â†’' (âˆ€' (( #0 is_successor_of' #1) â†’' (#0 âˆˆ' (x â†‘ 2 ï¼  0))))))
postfix ` is_inductive'`:100 := is_inductive 

@[simp] def unique_in_var0 (Ï†: formula ğ’®) : formula ğ’® 
  :=  âˆ€' âˆ€' (  (Ï†  â†‘ 1 ï¼  1 ) âˆ§' ( Ï† â†‘ 1 ï¼  0 ) â†’' (#0 =' #1) )

@[simp] def unique_ex (Ï† : formula ğ’®) : formula ğ’® 
  := (âˆƒ'Ï†) âˆ§' (unique_in_var0 Ï†)
prefix `âˆƒ!`:110 := unique_ex 

end zfc_language


/-
-- some notation for the pretty printer to make debugging easier
-- before
#check  #1 âˆˆ' #2                        -- #1 âˆˆ' #2 : formula ğ’®
#reduce #1 âˆˆ' #2                        -- ((pred pred_symb.elem).papp #1).papp #2
#reduce (#0 âˆˆ' #2 âˆ§' #1 âˆˆ' #2) â†‘ 1 ï¼  1 -- ((pred pred_symb.elem).papp #0).papp #3 âˆ§' ((pred pred_symb.elem).papp #2).papp #3
notation s ` 'âˆˆ `:100 t := papp (papp (pred pred_symb.elem) s) t
-- after
#check  #1 âˆˆ' #2                        -- #1 âˆˆ' #2 : formula ğ’®
#reduce #1 âˆˆ' #2                        -- #1 âˆˆ #2
#reduce (#0 âˆˆ' #2 âˆ§' #1 âˆˆ' #2) â†‘ 1 ï¼  1 -- (#0 âˆˆ #3) âˆ§' #2 âˆˆ #3
-- much better
-/


section zfc_axioms
namespace separation
/-
  The separation axiom scheme is defined as the closure* of
    `âˆ€A âˆƒB âˆ€x ( x âˆˆ B â†” x âˆˆ A âˆ§ Ï†)` (1)
  for all formulas `Ï†` such that `B` is not free in `Ï†`.
  Ignoring variable names we see that the existential quantifier 
  binds variables of `Ï†` pointing to `1`.
  We can avoid such bindings by simply requiring that 
    `Ï† = Ïˆ â†‘ 1 ï¼  1` 
  for a formula `Ïˆ`.
  On the other hand, if no index points to `1`, then one can check that
    `Ï† = Ï†[#0/1] â†‘ 1 ï¼  1`**,
  reducing the side condition to a question about lifts.
  As an added bonus this allows us to state the axiom of separation 
  without any side condition as the closure of the formula below.

  (*) See further below.
  (**) Exercise 1: Generalize this and proof it in lean.
-/
@[simp] def formula (Ï† : formula ğ’®): formula ğ’® 
  := âˆ€' âˆƒ' âˆ€' ((#0 âˆˆ' #1)  â†”' (#0 âˆˆ' #2 âˆ§' (Ï†  â†‘ 1 ï¼   1)))

/-
  To define the closure of (1) we usually state separation in terms of formulas Ï†(x, A, xâ‚, ... , xâ‚™)
  with free variables among (but not necessarily exactly) x, A, xâ‚, ... , xâ‚™.
  The closure is then given by
    `âˆ€xâ‚™ ... âˆ€xâ‚ âˆ€A âˆƒB âˆ€x ( x âˆˆ B â†” x âˆˆ A âˆ§ Ï†)`. (1')
  For our De Bruijn representation of variables this translates to `Ï†_h: closed (n+2) Ï†`,
  so `formula Ï†` has no n-free variables (see lemma below) and its closure (as defined in fol)
  gives us an De Bruijn version of (1') and invariance under lifts.
-/

lemma closed{k} {Ï†} (H: closed (k+2) Ï†) : closed k (formula Ï†)  :=
begin
  have hâ‚: Â¬ k + 3 â‰¤ 2, by linarith,
  have hâ‚‚: 1 â‰¤ k+2, by linarith,
  have hâ‚ƒ:  Ï† â†‘  1 ï¼   1 â†‘ 1  ï¼  (k + 3) = Ï† â†‘ 1 ï¼  1,
    begin  rw â†(lift_lift Ï† 1 1 hâ‚‚), congr, exact H, end,
  simp[hâ‚, hâ‚ƒ],
end

def sentence  (Ï† : fol.formula ğ’®) {n : â„•} (Ï†_h: formula.closed (n+2) Ï†) : fol.formula ğ’® 
  := formula.closure (formula Ï†) (closed Ï†_h)

lemma is_sentence {k : â„•} (Ï†  : fol.formula ğ’®) (H: fol.formula.closed (k+2) Ï†) :
  (sentence Ï† H) is_sentence' := begin exact closure_is_sentence (closed H) end

lemma lift_sentence (Ï†) (n) (Ï†_h: fol.formula.closed (n+2) Ï†) (m i) 
  : (sentence Ï† Ï†_h) â†‘ m ï¼   i = sentence Ï† Ï†_h := lift_sentence_id (is_sentence _ _)


/- To following definition and lemmas are used to make future proofs more explicit and readable. -/
lemma mem {Î“:set $ fol.formula ğ’®} (Ï†) (k) (Ï†_h: formula.closed (k+2) Ï†) 
  {Ïˆ} (h : Ïˆ = sentence Ï† Ï†_h) (H: (sentence Ï† Ï†_h) âˆˆ Î“) : Ïˆ âˆˆ Î“ :=
begin subst h, exact H, end 

def scheme : set $ fol.formula ğ’® := 
  { (sentence Ï† Ï†_h) |  (Ï† : fol.formula ğ’®) (k: â„•) (Ï†_h : formula.closed (k+2) Ï†) } 

lemma mem_scheme (Ï† : fol.formula ğ’®) {k : â„•} (Ï†_h: fol.formula.closed (k+2) Ï†)
  : sentence Ï† Ï†_h âˆˆ scheme := begin existsi [Ï†, k, Ï†_h], refl end
 
end separation


namespace replacement
/-
  The replacement axiom scheme is defined as the closure of
    `âˆ€A ( âˆ€x(x âˆˆ A â†’ âˆƒ!y Ï†) â†’ âˆƒB âˆ€x (x âˆˆ A â†’  âˆƒy (y âˆˆ B âˆ§ Ï†)` (1)
  for all formulas Ï† such that B is not free in Ï†.
  Observe that in this case the the quantifier before B binds variables pointing to 2.
  Thus the side condition can be stated as
    `Ï† = Ïˆ â†‘ 1 ï¼  2` 
  for a formula `Ïˆ` and we obtain our axiom scheme as the closure of the formula below.
-/
@[simp] def formula (Ï†: formula ğ’®) := 
  (âˆ€'( âˆ€'(#0 âˆˆ' #1 â†’' âˆƒ!Ï†) â†’' ( âˆƒ' âˆ€' ( #0 âˆˆ' #2 â†’' (âˆƒ' (#0 âˆˆ' #2 âˆ§' (Ï† â†‘ 1 ï¼  2))))))) 

/-
  To define the closure of (1) we usually state replacement in terms of formulas Ï†(x, y, A, xâ‚, ... , xâ‚™)
  with free variables among (but not necessarily exactly) x, y, A, xâ‚, ... , xâ‚™.

  This then translates to `Ï†_h: closed (n+3) Ï†` and we can proceed as in the case of separation.
-/
lemma closed {k} {Ï†} (H: closed (k+3) Ï†) : closed k (formula Ï†) :=
begin
  have : Â¬ k+4 â‰¤ 3, by linarith,
  have : Â¬ k+4 â‰¤ 2, by linarith,
  have : Â¬ k+3 â‰¤ 2, by linarith,
  have hâ‚ : 0 â‰¤ k+3, from (k+3).zero_le,
  have hâ‚‚ : 1 â‰¤ k+3, by linarith,
  have hâ‚ƒ : 2 â‰¤ k+3, by linarith,
  have Hâ‚ : (Ï† â†‘ 1 ï¼  0) â†‘ 1 ï¼   (k + 4) = Ï†  â†‘  1 ï¼  0, from
    begin rw â†(lift_lift Ï† 1 1 hâ‚), congr, exact H, end,
  have Hâ‚‚: (Ï† â†‘ 1 ï¼  1) â†‘ 1 ï¼  (k + 4) = Ï† â†‘ 1 ï¼  1, from
    begin rw â†(lift_lift Ï† 1 1 hâ‚‚), congr, exact H, end,
  have Hâ‚ƒ: (Ï† â†‘ 1 ï¼  2) â†‘ 1 ï¼  (k + 4) = Ï† â†‘ 1 ï¼  2, from
    begin rw â†(lift_lift Ï† 1 1 hâ‚ƒ), congr, exact H, end,
  rw closed at H, clear hâ‚‚,
  simp[*, closed],
end 

def sentence  (Ï† : fol.formula ğ’®) {n : â„•} (Ï†_h: formula.closed (n+3) Ï†) : fol.formula ğ’® 
  := formula.closure (formula Ï†) (closed Ï†_h)

lemma is_sentence  (Ï† : fol.formula ğ’®) {k : â„•} (H: fol.formula.closed (k+3) Ï†) :
  (sentence Ï† H) is_sentence' := begin exact closure_is_sentence (closed H) end

lemma lift_sentence (Ï†) (n) (Ï†_h: fol.formula.closed (n+3) Ï†) (m i) 
  : (sentence Ï† Ï†_h) â†‘ m ï¼   i = sentence Ï† Ï†_h := lift_sentence_id (is_sentence _ _)

/- To following definition and lemmas are used to make future proofs more explicit and readable. -/
lemma mem {Î“:set $ fol.formula ğ’®} {Ïˆ} (Ï†) {k} (Ï†_h: formula.closed (k+3) Ï†) 
  (h : Ïˆ = sentence Ï† Ï†_h) (H: (sentence Ï† Ï†_h) âˆˆ Î“) : Ïˆ âˆˆ Î“ :=
begin subst h, exact H end 

def scheme : set $ fol.formula ğ’® := 
  { (sentence Ï† Ï†_h) |  (Ï† : fol.formula ğ’®) (k: â„•) (Ï†_h : fol.formula.closed (k+3) Ï†) }

lemma mem_scheme (Ï† : fol.formula ğ’®) {k : â„•} (Ï†_h: fol.formula.closed (k+3) Ï†)
  : sentence Ï† Ï†_h âˆˆ scheme := begin existsi [Ï†, k, Ï†_h], refl, end

end replacement


/- âˆ€b âˆ€a (âˆ€x (x âˆˆ a â†” x âˆˆ b) â†’ a = b) -/
@[simp] def extensionality  : formula ğ’® := âˆ€' âˆ€' ( (âˆ€' (#0 âˆˆ' #1 â†”' #0 âˆˆ' #2)) â†’' (#0 =' #1) )
/- âˆ€b âˆ€a âˆƒA âˆ€x (x = a âˆ¨ x = b â†’ x âˆˆ A) -/
@[simp] def pair_ax         : formula ğ’® := âˆ€' âˆ€' âˆƒ' âˆ€' ( (#0 =' #2) âˆ¨' (#0 =' #3) â†’' (#0 âˆˆ' #1))
/- âˆ€F âˆƒA âˆ€x (âˆƒy (x âˆˆ y âˆ§ y âˆˆ x) â†’ x âˆˆ A)  -/
@[simp] def union_ax        : formula ğ’® := âˆ€' âˆƒ' âˆ€' ((âˆƒ'( #1 âˆˆ' #0 âˆ§' #0 âˆˆ' #3)) â†’' (#0 âˆˆ' #1) )
/- âˆ€y âˆƒA âˆ€x (x âŠ† y â†’ x âˆˆ A) -/
@[simp] def power_ax        : formula ğ’® := âˆ€' âˆƒ' âˆ€' ((#0 'âŠ† #2) â†’' (#0 âˆˆ' #1))
/-- âˆƒw ( w is inductive) -/
@[simp] def infinity_ax     : formula ğ’® := âˆƒ' (#0 is_inductive')
/- x ( Â¬(x is empty) â†’ âˆƒy(y âˆˆ x âˆ§ Â¬(âˆƒz (z âˆˆ y âˆ§ z âˆˆ x))) -/
@[simp] def regularity      : formula ğ’® := 
  âˆ€' (Â¬'(#0 is_empty') â†’' (âˆƒ' ( (#0 âˆˆ' #1) âˆ§' Â¬' âˆƒ'(#0 âˆˆ' #1 âˆ§' #0 âˆˆ' #2))))
/- For every set `X` of nonempty, pairwise disjoint sets, 
  there exists a set `Y` containg exactly one element of each element of `X`.

  âˆ€X (âˆ€x âˆ€y ( x âˆˆ X âˆ§ y âˆˆ X â†’ (Â¬(x is empty) âˆ§ (x=y âˆ¨ âˆ€z Â¬ (z âˆˆ x âˆ§ z âˆˆ y)))     
        â†’ âˆƒY âˆ€x (x âˆˆ X â†’ âˆƒ!z (z âˆˆ x âˆ§ z âˆˆ Y))    
-/
@[simp] def axiom_of_choice : formula ğ’® :=
  âˆ€' ( âˆ€' âˆ€' ( #0 âˆˆ' #2 âˆ§' #1 âˆˆ' #2 â†’' âˆƒ' (#0 âˆˆ' #1) âˆ§' ( #0 =' #1 âˆ¨'  âˆ€' ( Â¬'( (#0 âˆˆ' #1 âˆ§' #0 âˆˆ' #2 )))))
      â†’' âˆƒ' âˆ€' ( #0 âˆˆ' #2 â†’' âˆƒ! (#0 âˆˆ' #1 âˆ§' #0 âˆˆ' #2)))

/-- âˆ€A âˆƒB âˆ€x ( x âˆˆ B â†” x âˆˆ A âˆ§ Ï† â†‘ 1 ï¼  1) -/
@[simp] def separation_ax  (Ï† : formula ğ’®) {n} (Ï†_h: closed (n+2) Ï†) : formula ğ’®            
  := separation.sentence Ï† Ï†_h
/-- âˆ€A ( âˆ€x(x âˆˆ A â†’ âˆƒ!y Ï†) â†’ âˆƒB âˆ€x (x âˆˆ A â†’  âˆƒy (y âˆˆ B âˆ§ Ï†) -/
@[simp] def replacement_ax (Ï† : formula ğ’®) {n} (Ï†_h: closed (n+3) Ï†) : formula ğ’®            
  :=replacement.sentence Ï† Ï†_h


/-- The axioms of ZFC set theory as set. -/
def zfc_ax : set $ formula ğ’® := { extensionality, pair_ax, union_ax, power_ax, infinity_ax, 
                                  regularity, axiom_of_choice} 
                                    âˆª separation.scheme
                                    âˆª replacement.scheme

lemma zfc_ax_set_of_sentences: âˆ€ x âˆˆ zfc_ax, (x is_sentence') :=
begin
  intros Ï†  h,
  repeat{cases h,};
  try {unfold sentence closed, refl, },
  { cases h_h with n hn,
    cases hn with h hh,
    subst hh, apply separation.is_sentence, },
  { cases h_h with n hn,
    cases hn with h hh,
    subst hh, apply replacement.is_sentence, },
end

lemma lift_zfc_ax {m i} : (Î» Ï•: formula ğ’®, Ï• â†‘ m ï¼  i) '' zfc_ax = zfc_ax 
  := lift_set_of_sentences_id zfc_ax_set_of_sentences

/- We mainly use the following lemmas to make usage of axioms more explicit in the text. -/
-- for arbitrary sets
lemma extensionality_mem {Î“: set $ formula ğ’®}{Ï†}(h: Ï† = extensionality)(H: extensionality âˆˆ Î“) : Ï† âˆˆ Î“ :=
begin subst h, exact H end
lemma pair_ax_mem {Î“: set $ formula ğ’®} {Ï†} (h: Ï† = pair_ax) (H: pair_ax âˆˆ Î“)    : Ï† âˆˆ Î“ :=
begin subst h, exact H end
lemma union_ax_mem {Î“: set $ formula ğ’®} {Ï†} (h: Ï† = union_ax) (H: union_ax âˆˆ Î“) : Ï† âˆˆ Î“ :=
begin subst h, exact H end
lemma power_ax_mem {Î“: set $ formula ğ’®} {Ï†} (H: power_ax âˆˆ Î“) (h: Ï† = power_ax) : Ï† âˆˆ Î“ :=
begin subst h, exact H end
lemma infinity_ax_mem {Î“: set $ formula ğ’®} {Ï†} (h: Ï† = infinity_ax) (H: infinity_ax âˆˆ Î“)  : Ï† âˆˆ Î“ :=
begin subst h, exact H end
lemma regularity_mem {Î“: set $ formula ğ’®}{Ï†}(h: Ï† = regularity)(H: regularity âˆˆ Î“) : Ï† âˆˆ Î“ :=
begin subst h, exact H end
lemma aoc_mem {Î“: set $ formula ğ’®}{Ï†}(h: Ï† = axiom_of_choice)(H: axiom_of_choice âˆˆ Î“) : Ï† âˆˆ Î“ :=
begin subst h, exact H end

-- for zfc_ax
lemma extensionality_mem_zfc_ax : extensionality âˆˆ zfc_ax := by simp[-extensionality, zfc_ax]
lemma pair_ax_mem_zfc_ax : pair_ax âˆˆ zfc_ax := by simp[-pair_ax, zfc_ax]
lemma union_ax_mem_zfc_ax : union_ax âˆˆ zfc_ax := by simp[-union_ax, zfc_ax]
lemma power_ax_mem_zfc_ax : power_ax âˆˆ zfc_ax := by simp[-power_ax, zfc_ax]
lemma infinity_ax_mem_zfc_ax : infinity_ax âˆˆ zfc_ax := by simp[-infinity_ax, zfc_ax]
lemma regularity_mem_zfc_ax : regularity âˆˆ zfc_ax := by simp[zfc_ax]
lemma aoc_mem_zfc_ax : axiom_of_choice âˆˆ zfc_ax := by simp[zfc_ax]

namespace separation
lemma mem_zfc_ax (Ï† k) (Ï†_h: formula.closed (k+2) Ï†) : sentence Ï† Ï†_h âˆˆ zfc_ax :=
begin simp[-sentence, zfc_ax, mem_scheme], end
end separation

namespace replacement
lemma mem_zfc_ax (Ï† k) (Ï†_h: formula.closed (k+3) Ï†) : sentence Ï† Ï†_h âˆˆ zfc_ax :=
begin simp[-sentence, zfc_ax, mem_scheme], end
end replacement

end zfc_axioms

section zfc_proofs
/- 
  ### On comments inside the proofs
  The first proof is the only one with excessive use of comments/
  In the following proofs we will give readable goals 
  and the current variable environment, hoping that the context should be clear.

  for example the current goal might look like
    (Î» (Ï• : formula ğ’®), Ï• â†‘ 1 ï¼  0) ''
        (âˆ€'(#0 âˆˆ' #1 â†”' #0 =' #3 âˆ¨' #0 =' #3) >>
          (Î» (Ï• : formula ğ’®), Ï• â†‘ 1 ï¼  0) '' ((Î» (Ï• : formula ğ’®), Ï• â†‘ 1 ï¼  0) '' zfc_ax)) âŠ¢
      ((#0 âˆˆ' #1 â†’' #0 =' #3) â†‘ 1 ï¼  (0 + 1 + 1))[#0 â„ 0 + 1]
  while the comment reads
  `a {a,a} x âŠ¢ x âˆˆ {a,a} â†’ x = a` 
  * a {a,a} x are sets (usually with associated properties hidden in the context, or obvious from the name)
  * indices pointing to 0 talk about x
  * indices pointing to 1 talk about {a,a}
  * indices pointing to 2 talk about a
  * the goal should be read as `x âˆˆ {a,a} â†’ x = a` 
  -//- 
  Lastly we use "-- meta" to denote parts of a proof not directly involving terms of type `fol.proof`.
  This is usually the case at the leaves of of a natural deduction proof tree
  where we have to reason about formulas being equal or element of the context.
-/

/--
  A formal proof that for all sets `b,a` there exists a set containing exactly `a` and `b`.

  Informally: `zfc_ax âŠ¢ âˆ€b âˆ€a âˆƒA âˆ€x (x âˆˆ A â†” x=a âˆ¨ x=b)`
-/
def pairset_ex: zfc_ax âŠ¢ âˆ€' âˆ€' âˆƒ' âˆ€' ( (#0 âˆˆ' #1) â†”' (#0 =' #2) âˆ¨' (#0 =' #3)) :=
begin
  apply allI, -- given a
  apply allI, -- given b
  apply exE âˆ€'( (#0 =' #2) âˆ¨' (#0 =' #3) â†’' (#0 âˆˆ' #1)), -- let A be a set containing b and a,
  { -- such a set exists pair pairing: 
    apply allE' _ #0,         -- bind b   --(âˆƒ' âˆ€'( (#0 =' #2) âˆ§' (#0 =' #4) â†’' (#0 âˆˆ' #1))) #0,
    apply allE' _ #1,         -- bind a  --( âˆ€' âˆƒ' âˆ€'( (#0 =' #2) âˆ§' (#0 =' #3) â†’' (#0 âˆˆ' #1))) #1,
    apply hypI,               -- this is a hypothesis
    -- meta
    simp only [lift_zfc_ax],
    apply pair_ax_mem_zfc_ax,
    simp[zfc_ax],
    all_goals { simp } },
  { -- now we can use A and its defining property for further arguments
    -- reminder: sets (in order) a b A
    apply exE (âˆ€'( #0 âˆˆ' #1 â†”' (#0 âˆˆ' #2) âˆ§' (#0 =' #3 âˆ¨' #0 =' #4))), -- let {b,a} be the set {x | x âˆˆ A âˆ§ (x=b âˆ¨ x=a)}
    { -- such a set exists by separation:
      apply allE' _ #0,                                           -- bind A
      apply allE' _ #1,                                           -- bind b
      apply allE' _ #2,                                           -- bind a
      apply hypI,                                                 -- this is a hypothesis
      -- meta
      apply separation.mem (#0 =' #2 âˆ¨' #0 =' #3) 2 (rfl) (rfl),   -- an instance of separation
      simp only [lift_zfc_ax],
      right, apply separation.mem_zfc_ax,
      all_goals{ simp[alls] }, refl },
    { -- variables (in order) a b A { x | x âˆˆ A âˆ§ (x=b âˆ¨ x=a)}
      apply exI #0, -- put X := {b,a}:= { x | x âˆˆ A âˆ§ (x=b âˆ¨ x=a)} , we need to show that it satisfies the defining property
      apply allI,
      -- stack : a b A {b,a} x
      apply andI, 
      { -- âŠ¢ x âˆˆ {b,a} â†’ x=b âˆ¨ x=a
        apply impI, -- assume x âˆˆ {b,a} we want to show x=b âˆ¨ x=a
        apply andEâ‚‚ (#0 âˆˆ' #2), -- it suffices to show  x âˆˆ A âˆ§ (x=b âˆ¨ x=a)
        apply impE_insert,  -- moreover it suffices to show x âˆˆ {b,a} â†’  x âˆˆ A âˆ§ (x=b âˆ¨ x=a)
        apply iffE_r,      -- it suffices to show  x âˆˆ {b,a} â†” x âˆˆ A âˆ§ (x=b âˆ¨ x=a)
        apply allE_var0,   -- bind x
        apply hypI,         -- then this is a hypothesis, namely the defining property of  { x | x âˆˆ A âˆ§ (x=b âˆ¨ x=a)
        -- meta
        simp only [set.image_insert_eq], 
        apply set.mem_insert, },
      { -- âŠ¢ x âˆˆ {b,a} â† x=b âˆ¨ x=a
        apply impI,             -- assume x=b âˆ¨ x=a, we need to show x âˆˆ {b,a}
        apply impE (#0 âˆˆ' #2),  -- it suffices to show x âˆˆ A and x âˆˆ A â†’ x âˆˆ {b,a}
        { -- first we show x âˆˆ A
          apply impE ((#0 =' #3) âˆ¨' (#0 =' #4)), -- it suffices to show x = b âˆ¨ x=a and x = b âˆ¨ x=a â†’ x âˆˆ A
          { -- we show x = b âˆ¨ x = a
            apply hypI1 },  -- which is true by assumption
          { -- we show  x = b âˆ¨ x = a â†’ x âˆˆ A
            apply allE_var0, -- if we bind x
            apply hypI,       -- then this is how A was defined
            -- meta
            simp only [set.image_insert_eq],
            right, right, apply set.mem_insert } },
        { -- next we show x âˆˆ A â†’ x âˆˆ {b,a}
          apply impI,   -- assume x âˆˆ A
          apply impE (#0 âˆˆ' #2 âˆ§' ((#0 =' #3) âˆ¨' (#0 =' #4))), -- then it suffices to show ...
          { -- x âˆˆ A âˆ§ x=0 âˆ¨ x=b 
            apply andI, -- we need to show ...
            { -- âŠ¢ x âˆˆ A
              apply hypI1 }, -- by assumption
            { -- âŠ¢ x = 0 âˆ¨ x=b
              apply hypI2 } },-- by assumption
          { -- âŠ¢ x âˆˆ A âˆ§ x=0 âˆ¨ x=b â†’ x âˆˆ {b,a}
            apply iffE_l, -- it suffices to show x âˆˆ {b,a} â†” x âˆˆ A âˆ§ x=0 âˆ¨ x=b 
            apply allE_var0,  -- binding x
            apply hypI,        -- this how we defined {b,a} in the first place
            -- meta
            simp only [set.image_insert_eq], 
            right, right, apply set.mem_insert } } } } },
end

/--
  Formal proof that an empty set exists.

  Informally: `zfc_ax âŠ¢ âˆƒA (âˆ€x ( x âˆˆ A â†” xâ‰ x )) `
-/
def emptyset_ex : zfc_ax âŠ¢ âˆƒ' (#0 is_empty'):=
begin
  -- consider the set { x | x âˆˆ A âˆ§ Â¬'(#0 =' #0 ) }
  apply exE âˆ€'(#0 âˆˆ' #1 â†”' #0 âˆˆ' #2 âˆ§' Â¬'(#0 =' #0 )),
  { -- such a set exists by separation
      apply allE_var0,           -- bind A
      apply hypI,                 -- then this is true by separation
      apply separation.mem (Â¬'(#0 =' #0 )) 0 (rfl) (rfl),
      apply separation.mem_zfc_ax, },
  { -- âŠ¢ âˆƒX âˆ€x ( x âˆˆ X â†” Â¬ (x = x) ) 
    apply exI #0, -- Put X:={ x | x âˆˆ A âˆ§ Â¬'(#0 =' #0 ) }
    apply allI,   -- given x
    apply andI,
    { -- âŠ¢  x âˆˆ X â†’ Â¬ (x = x)
      apply impI,
      apply andEâ‚‚ _,
      apply impE_insert,
      apply iffE_r,
      apply allE_var0,
      apply hypI,
      -- meta
      simp only [set.image_insert_eq],
      left, refl },
    { -- âŠ¢ Â¬ (x = x) â†’  x âˆˆ X 
      apply impI,
      apply botE,
      apply impE (#0 =' #0),
      apply eqI,
      apply hypI,
      -- meta
      left, refl } },
end

/--
  Formal proof that for all sets `a` there exists a set containing just `a`.

  Informally: `zfc_ax âŠ¢ âˆ€a âˆƒA (âˆ€x ( x âˆˆ A â†” x = a )) `
-/
def singleton_ex : zfc_ax âŠ¢ âˆ€' âˆƒ' âˆ€' ( #0 âˆˆ' #1 â†”' #0 =' #3) :=
begin
  apply allI, --given a
  apply exE âˆ€' ( #0 âˆˆ' #1 â†”' #0 =' #3 âˆ¨' #0 =' #3), -- have the set {a,a}
  { -- a âŠ¢ âˆƒA ( A = {a,a}) 
    apply allE' _ #1,
    apply allE' _ #1,
    rw [lift_zfc_ax],
    apply pairset_ex,
    -- meta
    dsimp, refl,
    dsimp, refl },
  { -- a âŠ¢ âˆƒ A âˆ€x (x âˆˆ A â†” x = a)
    apply exI #0, -- put `A := {a,a}`
    apply allI,   -- given x
    apply andI,
    { -- a {a,a} x âŠ¢ x âˆˆ {a,a} â†’ x = a
      apply impI, -- assume `x âˆˆ {a,a}`
      apply orE (#0 =' #3) (#0 =' #3), -- suffices to show (x = a) âˆ¨ ( x = a)
      { -- a {a,a} x âŠ¢ x = a âˆ¨ x = a
        apply impE_insert,
        apply iffE_r,
        apply allE_var0,
        apply hypI,
        -- meta
        simp only [set.image_insert_eq], 
        left, refl },
      { -- assume `x = a`
        -- a {a,a} xâŠ¢ x = a
        apply hypI1 },
      { -- assume `x = a`
        -- a {a,a} x âŠ¢ x = a
        apply hypI1 } },
    { -- a {a,a} x âŠ¢ x = a â†’ x âˆˆ {a,a}
      apply impI, -- assume `x = a`
      apply impE ((#0 =' #3) âˆ¨' (#0 =' #3)),
      { -- a {a,a} âŠ¢ x=a âˆ¨ x=a
        apply orIâ‚,
        apply hypI1 },
      { -- a {a,a} x âŠ¢ x=a âˆ¨ x=a â†’ x âˆˆ {a,a}
        apply iffE_l,
        apply allE_var0,
        apply hypI,
        -- meta
        simp only [set.image_insert_eq], 
        right, left, refl } } }
end

/--
  Proof scheme. 
  Provides a formal proof of uniqueness of y
  satisfying formulas of the form `âˆ€x (x âˆˆ y â†” Ï† )`,
  provided `y` is not free in `Ï†`.

  Informally : {extensionality} âŠ¢ âˆ€yâ‚ âˆ€yâ‚€ ( yâ‚€ = { x | Ï† } âˆ§ yâ‚ = { x | Ï† } â†’ yâ‚€ = yâ‚)
-/
def extensionality_implies_uniqueness (Ï† : formula ğ’®)
  : {extensionality} âŠ¢ unique_in_var0  âˆ€'(#0 âˆˆ' #1 â†”' (Ï† â†‘ 1 ï¼  1)) :=
begin
  apply allI, -- yâ‚
  apply allI, -- yâ‚€
  apply impI, -- assume `âˆ€ x ( x âˆˆ yâ‚€ â†” Ï†(x, yâ‚€)) âˆ§ âˆ€ x (x âˆˆ yâ‚ â†” Ï†(x,yâ‚))`
  apply impE (âˆ€' (#0 âˆˆ' #1 â†”' #0 âˆˆ' #2)), 
  { -- yâ‚ yâ‚€ âŠ¢ (âˆ€' (#0 âˆˆ' #1 â†”' #0 âˆˆ' #2))
    apply allI, -- x
    apply iffI_trans (Ï† â†‘ 2 ï¼   1), 
    { -- yâ‚ yâ‚€ x âŠ¢ x âˆˆ yâ‚€ â†” Ï† (x, yâ‚€)
      apply allE_var0, 
      apply andEâ‚ _ , 
      apply hypI, 
      -- meta argument
      simp [set.image_insert_eq],
      simp [subst_var0_for_0_lift_by_1, lift_lift_merge Ï† 1] },
    { -- yâ‚ yâ‚€ x âŠ¢ Ï† (x, yâ‚) â†” x âˆˆ yâ‚
      apply iffI_symm, 
      apply allE_var0, 
      apply andEâ‚‚ _ , 
      apply hypI,
      -- meta argument
      simp [set.image_insert_eq] } },
  { -- yâ‚ yâ‚€ âŠ¢ âˆ€ x (x âˆˆ yâ‚€ â†” x âˆˆ yâ‚) â†’ yâ‚€ = yâ‚
    apply allE_var0,
    apply allE' _ #1,
    apply weak1,
    apply hypI, 
    -- meta argument
    simp,
    simp, },
end

/--
  QoL version of `extensionality_implies_uniqueness`

  Informally : `Î“ âŠ¢ âˆ€yâ‚ âˆ€yâ‚€ (Ïˆ(yâ‚€) âˆ§ Ïˆ(yâ‚) â†’ yâ‚€ = yâ‚`,
  provided  `Ïˆ(y) = âˆ€x (x âˆˆ y â†” Ï† )`, `y` not free in `Ï†` and `extensionality âˆˆ Î“`.
-/
def extensionality_implies_uniqueness' {Î“} (Ï†) {Ïˆ} (h: Ïˆ = âˆ€'(#0 âˆˆ' #1 â†”' (Ï† â†‘ 1 ï¼  1) ) ) (H: extensionality âˆˆ Î“)  
  : Î“ âŠ¢ unique_in_var0 Ïˆ :=
begin
  subst h,
  apply weak_singleton extensionality (extensionality_implies_uniqueness Ï†),
  exact H,
end

/--
  `n`-closure variant of `extensionality_implies_uniqueness` 

  Informally : `{extensionality} âŠ¢ âˆ€xâ‚™ ... âˆ€xâ‚ âˆ€yâ‚ âˆ€yâ‚€ ( yâ‚€ = { x | Ï† } âˆ§ yâ‚ = { x | Ï† } â†’ yâ‚€ = yâ‚)`
-/
def extensionality_implies_uniqueness_alls  (n)  (Ï† : formula ğ’®)
  : {extensionality} âŠ¢ alls n (unique_in_var0 âˆ€'(#0 âˆˆ' #1 â†”' (Ï† â†‘ 1 ï¼  1))) :=
begin
  apply allsI,
  apply extensionality_implies_uniqueness' Ï† (rfl),
  rw set.mem_image,
  use extensionality,
  exact âŸ¨ set.mem_singleton _ , rfl âŸ©,
end

/--
  Formal proof that for all sets `b,a` the pair set `{a,b}` exists and is unique.

  Informally: `zfc_ax âŠ¢ âˆ€b âˆ€a âˆƒ!A (âˆ€x (x âˆˆ A â†” x = a âˆ¨ x = b))`
-/
def pairset_unique_ex : zfc_ax âŠ¢ (âˆ€' âˆ€' âˆƒ! âˆ€' ((#0 âˆˆ' #1) â†”' (#0 =' #2) âˆ¨' (#0 =' #3))) := 
begin
  apply allI, -- b
  apply allI, -- a
  simp only [lift_zfc_ax],
  apply andI,
  { -- b a âŠ¢ âˆƒA (âˆ€x (x âˆˆ A â†” x = a âˆ¨ x = b)) 
    apply allE' _ #0,
    apply allE' _ #1,
    exact pairset_ex,
    simp, simp },
  { apply extensionality_implies_uniqueness' (#0 =' #1 âˆ¨' #0 =' #2) (rfl),
    simp[-extensionality, zfc_ax] },
end
/--
  Formal proof that there exists an unique empty set.

  Informally: `zfc_ax âŠ¢ âˆƒ! A (âˆ€x ( x âˆˆ A â†” Â¬(x=x))) `
-/
def emptyset_unique_ex : zfc_ax âŠ¢ âˆƒ! (#0 is_empty') := 
begin
  apply andI,
  { exact emptyset_ex, },
  { apply extensionality_implies_uniqueness' (Â¬'(#0 =' #0)) (rfl),
    simp[-extensionality, zfc_ax] },
end

/--
  Formal proof that for all sets `a` the singleton `{a}` exists and is unique.

  Informally: `zfc_ax âŠ¢ âˆ€a âˆƒ!A (A = {a}) `
-/
def singleton_unique_ex : zfc_ax âŠ¢ âˆ€' âˆƒ! âˆ€' ( #0 âˆˆ' #1 â†”' #0 =' #3) :=
begin
  apply allsI 1,
  apply andI,
  { apply allsE' 1,
    exact singleton_ex, },
  { apply extensionality_implies_uniqueness' (#0 =' #2) (rfl),
    simp only [lift_zfc_ax],
    simp[-extensionality, zfc_ax] },
end

/--
  Proof scheme. Provides a formal proof of `âˆƒB âˆ€x(x âˆˆ B â†” Ï†)`
  from `âˆƒB âˆ€x ( Ï† â†’ x âˆˆ B)` by using the axiom of separation for `Ï†`.
-/
def separation_proof_scheme 
  (Ï† k) (Ï†_hâ‚: closed (k+2) Ï†)              -- given a formula Ï†(x_1,...,x_{k+1})
  (Ï†_hâ‚‚ : âˆƒ Ï• : formula ğ’® , Ï† = Ï• â†‘ 1 ï¼  1) -- such that the xâ‚‚ is not among its free variables
  {Î“} (h : separation_ax Ï† Ï†_hâ‚ âˆˆ Î“)        -- ...
  (H : Î“ âŠ¢ alls k âˆƒ' âˆ€'(Ï† â†’' (#0 âˆˆ' #1)))
  : Î“ âŠ¢ alls k (âˆƒ' âˆ€'((#0 âˆˆ' #1) â†”' Ï†)) :=
begin
  apply allsI,
  apply exE âˆ€'( Ï† â†’' (#0 âˆˆ' #1)), -- A with âˆ€ x (Ï† â†’ x âˆˆ A) 
  { -- xâ‚– ... xâ‚ âŠ¢ âˆƒ A âˆ€x (Ï† â†’ x âˆˆ A)
    apply allsE',
    exact H },
  { -- xâ‚– ... xâ‚ A âŠ¢ âˆƒ B âˆ€ x (x âˆˆ B â†” Ï† )
    apply exE (âˆ€'( (#0 âˆˆ' #1) â†”' ( (#0 âˆˆ' #2) âˆ§' (Ï† â†‘ 1 ï¼  1 ) ))), -- B with âˆ€ x (x âˆˆ B â†” x âˆˆ A âˆ§ Ï† )
    { -- xâ‚– ... xâ‚ A âŠ¢ âˆƒ B âˆ€ x (x âˆˆ B â†” x âˆˆ A âˆ§ Ï† )
      apply weak1, 
      apply allsE' 1,
      apply allsE' k,
      rw [alls,alls],
      apply hypI,
      -- meta
      apply separation.mem Ï† k Ï†_hâ‚ (rfl),
      assumption, },
    { -- A B âŠ¢ âˆƒ B âˆ€ x (x âˆˆ B â†” Ï† ) 
      apply exI #0,
      apply allI, -- x
      apply andI,
      { -- A B x âŠ¢ x âˆˆ B â†’ Ï†
        apply impI, -- assume `x âˆˆ B`
        apply andEâ‚‚ (#0 âˆˆ' #2),
        apply impE_insert,
        apply iffE_r,
        apply allE_var0,
        apply hypI,
        -- meta
        rw[set.image_insert_eq],
        left,
        cases Ï†_hâ‚‚ with Ïˆ Ïˆ_h,
        subst Ïˆ_h,
        rw [subst_var0_lift_by_1, subst_var0_lift_by_1],
        rw [â†lift_lift Ïˆ _ _ (le_refl 1)], 
        refl },
      { --  A B x âŠ¢ Ï† â†’ x âˆˆ B
        apply impI, -- assume `Ï†`
        apply impE (#0 âˆˆ' #2),
        { --  A B x âŠ¢ x âˆˆ A
          apply impE (Ï† â†‘ 1 ï¼  1),
          { -- A B x âŠ¢ Ï† 
            apply hypI,
            left,
            cases Ï†_hâ‚‚ with Ïˆ Ïˆ_h,
            subst Ïˆ_h,
            rw [subst_var0_lift_by_1, â†lift_lift Ïˆ _ _ (le_rfl)] },
          { -- A B x âŠ¢ Ï† â†’ x âˆˆ A
            apply allE_var0, 
            apply hypI,
            -- meta
            simp only [set.image_insert_eq],
            right, right, left, refl } },
        { --  A B x âŠ¢ x âˆˆ A â†’ x âˆˆ B
          apply impI, -- assume `x âˆˆ A` 
          apply impE (#0 âˆˆ' #2 âˆ§' (Ï† â†‘ 1 ï¼  1)),
          { -- A B x âŠ¢ x âˆˆ A âˆ§ Ï† 
            apply andI, 
            { -- A B x âŠ¢ x âˆˆ A
              apply hypI1 },
            { -- A B x âŠ¢ Ï† 
              apply hypI,
              -- meta
              right, left,
              cases Ï†_hâ‚‚ with Ïˆ Ïˆ_h,
              subst Ïˆ_h,
              rw [subst_var0_lift_by_1, lift_lift Ïˆ _ _ (le_rfl)] } },
          { -- A B x âŠ¢  x âˆˆ A âˆ§ Ï† â†’ x âˆˆ B
            apply iffE_l, 
            apply allE_var0, 
            apply hypI,
            --meta
            simp only [set.image_insert_eq], 
            right, right, left,
            simp } } } } }
end


/--
  QoL version of `separation_proof_scheme`.
  
  Proof scheme. Provides a formal proof `Ïˆ`
  from `âˆƒB âˆ€x ( Ï† â†’ x âˆˆ B)` and `Ïˆ = âˆƒB âˆ€x(x âˆˆ B â†” Ï†)`.
-/
def separation_proof_scheme' (Ï†) (k) (Ï†_h: closed (k+2) (Ï† â†‘ 1 ï¼  1))
  {Ïˆ : formula ğ’®} (Ïˆ_h : Ïˆ = alls k âˆƒ' âˆ€'((#0 âˆˆ' #1) â†”' (Ï†  â†‘ 1 ï¼  1)))
  {Î“} (h : separation.sentence (Ï† â†‘ 1 ï¼  1) Ï†_h âˆˆ Î“)
  (H: Î“ âŠ¢ alls k âˆƒ' âˆ€'( Ï†  â†‘ 1 ï¼  1  â†’' (#0 âˆˆ' #1))) 
  : Î“ âŠ¢ Ïˆ  :=
begin
  subst Ïˆ_h,
  apply separation_proof_scheme (Ï† â†‘ 1 ï¼  1) k Ï†_h (by use Ï†) h H,
end

/--
  Formal proof that for all sets `F` there exists a set 
  containing exactly the elements of its elements.

  Informally : `zfc_ax âŠ¢ âˆ€F âˆƒA âˆ€x (x âˆˆ A â†” âˆƒy (x âˆˆ y âˆ§ y âˆˆ F))`
-/
def union_ex : zfc_ax âŠ¢ âˆ€' âˆƒ' âˆ€' ( (#0 âˆˆ' #1) â†”' âˆƒ'(#1 âˆˆ' #0 âˆ§' #0 âˆˆ' #3) ):=
begin
  apply separation_proof_scheme' (âˆƒ'(#1 âˆˆ' #0 âˆ§' #0 âˆˆ' #2)) 1, -- enough to show one direction,
  { refl, },                                                    
  { apply separation.mem_zfc_ax, },                             -- which is an axiom
  { apply hypI,
    apply union_ax_mem_zfc_ax },
  { dsimp, refl, },
end

/--
  Formal proof that for all sets `F` the union `â‹ƒF` exists and is unique.

  Informally : `zfc_ax âŠ¢ âˆ€F âˆƒ!A (A = â‹ƒF)`
-/
def union_unique_ex : zfc_ax âŠ¢ âˆ€' âˆƒ! âˆ€' ( (#0 âˆˆ' #1) â†”' âˆƒ'(#1 âˆˆ' #0 âˆ§' #0 âˆˆ' #3) ) := 
begin
  apply allI,
  simp only [lift_zfc_ax],
  apply andI,
  { apply allE' _ #0,
    exact union_ex,
    simp, },
  { apply extensionality_implies_uniqueness' (âˆƒ'(#1 âˆˆ' #0 âˆ§' #0 âˆˆ' #2)) (rfl),
    simp[-extensionality, zfc_ax] },
end

/--
  Formal proof that for all sets `y`  there exists a set 
  containing exactly all its subsets exists.

  Informally : `zfc_ax âŠ¢ âˆ€y âˆƒ!A âˆ€x (x âˆˆ A  â†” x âŠ† y)`
-/
def powerset_ex: zfc_ax âŠ¢ âˆ€' âˆƒ' âˆ€' ((#0 âˆˆ' #1) â†”' ( #0 'âŠ† #2)) :=
begin
  apply separation_proof_scheme' (#0 'âŠ† #1) 1,      -- enough to show oen direction
  { refl },
  { apply separation.mem_zfc_ax, },                  -- which is an axiom
  { apply hypI,
    apply power_ax_mem_zfc_ax, },
  { dsimp, refl, },
end

/--
  Formal proof that for all sets `y` the powerset `ğ’«y` exists and is unique.

  Informally : `zfc_ax âŠ¢ âˆ€y âˆƒ!A (A = ğ’«y)`
-/
def powerset_unique_ex : zfc_ax âŠ¢ âˆ€' âˆƒ! âˆ€' ((#0 âˆˆ' #1) â†”' ( #0 'âŠ† #2)) := 
begin
  apply allI,
  simp only [lift_zfc_ax],
  apply andI,
  { apply allE_var0,
    exact powerset_ex, },
  { apply extensionality_implies_uniqueness' (#0 'âŠ† #1) (rfl),
    simp[-extensionality, zfc_ax] },
end

/--
  Formal proof that for all sets `b, a` there exists a set containing exactly 
  the elements of `a` and `b`.

  Informally: `zfc_ax âŠ¢ âˆ€b âˆ€a âˆƒA âˆ€ x (x âˆˆ A  â†” x âˆˆ a âˆ¨ x âˆˆ b)`
-/
def binary_union_ex : zfc_ax âŠ¢ âˆ€' âˆ€' âˆƒ' âˆ€' (#0 âˆˆ' #1 â†”' #0 âˆˆ' #2 âˆ¨' #0 âˆˆ' #3) :=
begin
  apply separation_proof_scheme' (#0 âˆˆ' #1 âˆ¨' #0 âˆˆ' #2) 2, -- only need to show one direction
  { refl, },
  { apply separation.mem_zfc_ax, },
  { apply allI, -- b
    apply allI, -- a
    apply exE âˆ€'((#0 âˆˆ' #1) â†”' (#0 =' #2) âˆ¨' (#0 =' #3)),
    { -- b a âŠ¢ âˆƒB (B = {a,b}) 
      apply allE' _ #0,
      apply allE' _ #1,
      simp only [lift_zfc_ax],
      exact pairset_ex,
      simp, simp },
    { -- b a {a,b} âŠ¢ âˆƒA âˆ€x ( x âˆˆ a  âˆ¨ x âˆˆ b â†’ x âˆˆ A) 
      apply exE  âˆ€'( (#0 âˆˆ' #1) â†”' âˆƒ'(#1 âˆˆ' #0 âˆ§' #0 âˆˆ' #3)),
      { -- b a {a,b} âŠ¢ âˆƒB (B = â‹ƒ{a,b})
        apply allE' _ #0,
        simp only [lift_zfc_ax],
        apply weak1,
        exact union_ex,
        simp },
      { -- b a {a,b} â‹ƒ{a,b} âŠ¢ âˆƒA âˆ€x ( x âˆˆ a  âˆ¨ x âˆˆ b â†’ x âˆˆ A) 
        apply exI #0, -- let `A := â‹ƒ{a,b}`
        apply allI, -- x
        apply impI, -- assume `x âˆˆ a âˆ¨ x âˆˆ b`
        apply orE (#0 âˆˆ' #3)  (#0 âˆˆ' #4),
        { apply hypI1 },
        { -- assume `x âˆˆ a`
          -- b a {a,b} â‹ƒ{a,b} âŠ¢ x âˆˆ â‹ƒ{a,b}
          apply impE (âˆƒ'(#1 âˆˆ' #0 âˆ§' #0 âˆˆ' #3)),
          { -- b a {a,b} â‹ƒ{a,b} âŠ¢ âˆƒy (x âˆˆ y âˆ§  y âˆˆ {a,b})
            apply exI #3,  -- put `y:= a`
            apply andI,
            { apply hypI1, },
            { -- b a {a,b} â‹ƒ{a,b} âŠ¢ a âˆˆ {a,b}
              apply impE (#3 =' #3 âˆ¨' #3 =' #4),
              { -- b a {a,b} â‹ƒ{a,b} y âŠ¢ a = a âˆ¨ a = b
                apply orIâ‚, 
                apply eqI, },
              { -- b a {a,b} â‹ƒ{a,b} âŠ¢ a = a âˆ¨ a = b â†’ a âˆˆ {a,b}
                apply iffE_l,
                apply allE' _ #3,
                apply hypI,
                -- meta
                simp only [set.image_insert_eq],
                right, right, right, left, refl,
                simp } } },
          { -- b a {a,b} â‹ƒ{a,b} y âŠ¢ âˆƒ(x âˆˆ y âˆ§  y âˆˆ {a,b}) â†’ x âˆˆ â‹ƒ{a,b}
            apply iffE_l,
            apply allE_var0,
            apply hypI,
            -- meta
            simp only [set.image_insert_eq],
            right, right, left, refl } },
        { -- assume `x âˆˆ b`
          -- b a {a,b} â‹ƒ{a,b} âŠ¢ x âˆˆ â‹ƒ{a,b}
          apply impE (âˆƒ'(#1 âˆˆ' #0 âˆ§' #0 âˆˆ' #3)),
          { -- -- b a {a,b} â‹ƒ{a,b} âŠ¢ âˆƒy (x âˆˆ y âˆ§  y âˆˆ {a,b})
            apply exI #4, -- put `y:=b`
            apply andI,
            { apply hypI1, },
            { -- b a {a,b} â‹ƒ{a,b} âŠ¢ b âˆˆ {a,b}
              apply impE (#4 =' #3 âˆ¨' #4 =' #4),
              { -- b a {a,b} â‹ƒ{a,b} y âŠ¢ b = a âˆ¨ b = b
                apply orIâ‚‚, 
                apply eqI, },
              { -- b a {a,b} â‹ƒ{a,b} âŠ¢ b = a âˆ¨ b = b â†’ b âˆˆ {a,b}
                apply iffE_l,
                apply allE' _ #4,
                apply hypI,
                -- meta
                simp only [set.image_insert_eq],
                right, right, right, left, refl,
                simp } } },
          { -- b a {a,b} â‹ƒ{a,b} y âŠ¢ âˆƒ(x âˆˆ y âˆ§  y âˆˆ {a,b}) â†’ x âˆˆ â‹ƒ{a,b}
            apply iffE_l,
            apply allE_var0,
            apply hypI,
            -- meta
            simp only [set.image_insert_eq],
            right, right, left, refl } } } } },
  { dsimp, refl, },
end


/--
  Formal proof that for all sets `b, a` the binary union `a âˆª b` exists and is unique.

  Informally: `zfc_ax âŠ¢ âˆ€b âˆ€a âˆƒ!A (A = a âˆª b)`
-/
def binary_union_unique_ex : zfc_ax âŠ¢ âˆ€' âˆ€' âˆƒ! âˆ€' (#0 âˆˆ' #1 â†”' #0 âˆˆ' #2 âˆ¨' #0 âˆˆ' #3) := 
begin
  apply allsI 2,
  apply andI,
  { apply allsE' 2,
    exact binary_union_ex,},
  { apply extensionality_implies_uniqueness' (#0 âˆˆ' #1 âˆ¨' #0 âˆˆ' #2) (rfl),
    simp only [lift_zfc_ax],
    simp[-extensionality, zfc_ax] }
end

/--
  Formal proof that for all sets `a` there exists a successor set containing exactly `a` and
  the elements of `a` .

  Informally: `zfc_ax âŠ¢ âˆ€a âˆƒA âˆ€x ( x âˆˆ A â†” x âˆˆ a âˆ¨ x = a )`
-/
def successor_ex : zfc_ax âŠ¢ âˆ€' âˆƒ' (#0 is_successor_of' #1) :=
begin
  apply separation_proof_scheme' (#0 âˆˆ' #1  âˆ¨' (#0 =' #1)) 1, -- only need to show one direction
  { refl, },
  { apply separation.mem_zfc_ax, },
  { apply allI, -- a
    apply exE âˆ€' (#0 âˆˆ' #1 â†”' #0 =' #2),
    { -- a âŠ¢ âˆƒ A  (A = {a})
      apply allE' _ #0,
      simp only [lift_zfc_ax],
      exact singleton_ex, 
      dsimp, refl, },
    { -- a {a} âŠ¢ âˆƒ S âˆ€ x ( x âˆˆ a âˆ¨ x = {a} â†’ x âˆˆ S)
      apply exE âˆ€'(#0 âˆˆ' #1 â†”' #0 âˆˆ' #3 âˆ¨' #0 âˆˆ' #2),
      { -- a {a} âŠ¢ âˆƒ B ( B = a âˆª {a} )
        apply allE' _ #1,
        apply allE' _ #0,
        simp only [lift_zfc_ax],
        apply weak1,
        exact binary_union_ex,
        simp, dsimp, refl },
      { -- a {a} (a âˆª {a}) âŠ¢ âˆƒ S âˆ€ x ( x âˆˆ a âˆ¨ x = {a} â†’ x âˆˆ S)
        apply exI #0, -- put `S = a âˆª {a}`
        apply allI,   -- x
        apply impI,   -- assume `x âˆˆ a âˆ¨ x = a`
        apply orE (#0 âˆˆ' #3) (#0 =' #3),
        { apply hypI1, },
        { -- case `x âˆˆ a`
          -- a {a} (a âˆª {a}) x âŠ¢ x âˆˆ a âˆª {a}
          apply impE (#0 âˆˆ' #3 âˆ¨' #0 âˆˆ' #2),
          { -- a {a} (a âˆª {a}) x âŠ¢  x âˆˆ a âˆ¨ x âˆˆ {a}
            apply orIâ‚, 
            apply hypI1 },
          { -- a {a} (a âˆª {a}) x âŠ¢ x âˆˆ a âˆ¨ x âˆˆ {a} â†’ x âˆˆ a âˆª {a}
            apply iffE_l, 
            apply allE_var0,
            apply hypI,
            -- meta
            simp only [set.image_insert_eq],
            right, right, left, refl } },
        { -- case `x = a`
          -- a {a} (a âˆª {a}) x âŠ¢ x âˆˆ a âˆª {a} 
          apply impE (#0 âˆˆ' #3 âˆ¨' #0 âˆˆ' #2),
          { -- a {a} (a âˆª {a}) x âŠ¢  x âˆˆ a âˆ¨ x âˆˆ {a}
            apply orIâ‚‚, 
            apply impE_insert,
            apply iffE_l,
            apply allE_var0,
            apply hypI,
            -- meta
            simp only [set.image_insert_eq],
            right, right, left, refl },
          { -- a {a} (a âˆª {a}) x âŠ¢  x âˆˆ a âˆ¨ x âˆˆ {a} â†’ x âˆˆ a âˆª {a}
            apply iffE_l,
            apply allE_var0,
            apply hypI,
            -- meta
            simp only [set.image_insert_eq],
            right, right, left, refl } } } } },
  { dsimp, refl, },
end

/--
  Formal proof that for all sets `a` the successor set `S(a)` exists and is unique.

  Informally: `zfc_ax âŠ¢ âˆ€a âˆƒ!A ( A = S(a) )`
-/
def successor_unique_ex : zfc_ax âŠ¢ âˆ€' âˆƒ! (#0 is_successor_of' #1) := 
begin
  apply allsI 1,
  apply andI,
  { apply allsE' 1,
    exact successor_ex, },
  { apply extensionality_implies_uniqueness' (#0 âˆˆ' #1  âˆ¨' (#0 =' #1)) (rfl),
    simp only [lift_zfc_ax],
    simp[-extensionality, zfc_ax] },
end

/--
  Formal proof that there exists a set containing exactly the elements common to all inductive sets.

  Informally: `zfc_ax âŠ¢  âˆƒA âˆ€x (x âˆˆ A â†” âˆ€ w (w is inductive â†’ x âˆˆ w)`
-/
def omega_ex : zfc_ax âŠ¢ âˆƒ' âˆ€' ( #0 âˆˆ' #1 â†”' âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)) :=
begin
  apply separation_proof_scheme' (âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)) 0, -- enough to show one direction
  { refl, },
  { apply separation.mem_zfc_ax, },
  { apply exE (#0 is_inductive'),
    { -- âŠ¢ âˆƒA ( A is inductive)
      apply hypI,
      exact infinity_ax_mem_zfc_ax }, -- this exists because of the axiom of infinity
    { -- A âŠ¢ âˆƒÏ‰ âˆ€x (âˆ€w (w is inductive â†’ w âˆˆ Ï‰)) â†’ x âˆˆ Ï‰)
      apply exE âˆ€'(#0 âˆˆ' #1 â†”' #0 âˆˆ' #2  âˆ§'  âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)),
      { -- A âŠ¢ âˆƒB âˆ€x ( x âˆˆ B â†” (x âˆˆ A) âˆ§ âˆ€w (w is inductive â†’ x âˆˆ w) 
        apply allE_var0,
        apply hypI,
        -- meta
        simp only [lift_zfc_ax],
        right,
        apply separation.mem_zfc_ax (âˆ€'(#0 is_inductive' â†’' #1 âˆˆ' #0)) 0,
        dsimp, refl },
      { -- B with `âˆ€x ( x âˆˆ B â†” ((x âˆˆ A) âˆ§ âˆ€w (w is inductive â†’ x âˆˆ w)))` 
        -- A B âŠ¢ âˆƒÏ‰ âˆ€x  (âˆ€w (w is inductive â†’ x âˆˆ Ï‰)) â†’ x âˆˆ Ï‰)
        apply exI #0, -- let `Ï‰ := B`
        apply allI,
        apply impI,   -- assume `x` with `(âˆ€w (w is inductive â†’ x âˆˆ Ï‰))` 
        apply iffEâ‚ (#0 âˆˆ' #2 âˆ§' âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)),
        { -- A B x âŠ¢ (x âˆˆ A âˆ§ âˆ€w(w is inductive â†’ x âˆˆ w)
          apply andI,
          { -- A B x âŠ¢ x âˆˆ A 
            apply impE (#2 is_inductive'),
            { -- A B x âŠ¢ A is inductive 
              apply hypI,
              -- meta
              simp only [set.image_insert_eq],
              right, right, left,
              simp[is_inductive, is_empty, is_successor_of] },
            { -- A B x âŠ¢  A is inductive â†’ x âˆˆ A 
              apply allE' _ #2,
              apply hypI,
              -- meta
              left, refl, dsimp, refl } },
          { -- A B x âŠ¢ âˆ€w(w is inductive â†’ x âˆˆ w)
            apply hypI1, } },
        { -- A B x âŠ¢ x âˆˆ B â†” (x âˆˆ A âˆ§ âˆ€w(w is inductive â†’ x âˆˆ w))
          apply allE_var0,
          apply hypI,
          -- meta
          simp only [set.image_insert_eq],
          right, left, refl } } } },
  { -- meta
    dsimp, refl }
end 

/--
  Formal proof that `Ï‰` exists and is unique. 

  Informally: `zfc_ax âŠ¢  âˆƒ!A (A = Ï‰)`
-/
def omega_unique_ex : zfc_ax âŠ¢ âˆƒ! âˆ€' ( #0 âˆˆ' #1 â†”' âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)) :=
begin
  apply andI,
  { exact omega_ex, },
  { apply extensionality_implies_uniqueness' (âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)) (rfl),
    simp[-extensionality, zfc_ax] },
end

/--
  A formal proof that `Ï‰` is a subset of all inductive sets.

  Informally : `zfc_ax âŠ¢ âˆ€ A (A = Ï‰ â†’ âˆ€ w (w is inductive â†’ A âŠ† w))`
-/
def omega_subset_all_inductive : 
  zfc_ax âŠ¢ âˆ€' (âˆ€'( #0 âˆˆ' #1 â†”' âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)) â†’' âˆ€' (#0 is_inductive' â†’' #1 'âŠ† #0) )  :=
begin
  apply allI,
  apply impI,
  apply allI,
  apply impI,
  apply allI,
  apply impI,
  apply impE (#1 is_inductive'),
  { apply hypI, simp only [set.image_insert_eq], right, left, refl },
  { apply allE' _ #1,
    apply iffEâ‚‚ ( #0 âˆˆ' #2),
    { apply hypI1, },
    { apply allE_var0,
      apply hypI,
      simp only [set.image_insert_eq], 
      right, right, left, refl },
    { dsimp, refl, } },
end

/--
  A formal proof that `Ï‰` is inductive.

  Informally : `zfc_ax âŠ¢ âˆ€ A (A = Ï‰ â†’ A is inductive)`
-/
def omega_inductive : zfc_ax âŠ¢ âˆ€' (âˆ€'( #0 âˆˆ' #1 â†”' 
  âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)) â†’' (#0 is_inductive')) :=
begin
  apply allI, -- Ï‰
  apply impI, -- assume `Ï‰ = { x | âˆ€ w ( w is inductive â†’ x âˆˆ w) }`
  apply andI,
  { -- Ï‰ âŠ¢ âˆ€ x ( x is empty â†’ x âˆˆ Ï‰)
    apply allI, -- âˆ…
    apply impI, -- assume `âˆ… is empty`
    apply iffEâ‚ âˆ€'(#0 is_inductive' â†’' #1 âˆˆ' #0),
    { -- Ï‰ âˆ… âŠ¢ âˆ€ w (w is inductive â†’ âˆ… âˆˆ w)
      apply allI, -- w
      apply impI, -- assume `w is inductive`
      apply impE (#1 is_empty'),
      { -- Ï‰ âˆ… w âŠ¢ âˆ… is empty
        apply hypI, 
        simp only [set.image_insert_eq], 
        right, left, refl, },
      { -- Ï‰ âˆ… w âŠ¢ âˆ… is empty â†’ âˆ… âˆˆ w
        apply allE' _ #1,
        apply andEâ‚,
        apply hypI1,
        unfold is_empty, refl } },
    { -- Ï‰ âˆ… âŠ¢ âˆ… âˆˆ Ï‰ â†” âˆ€ w (w is inductive â†’ âˆ… âˆˆ w)
      apply allE_var0,
      apply hypI,
      simp only[set.image_insert_eq],
      right, left, refl } },
  { -- Ï‰ âŠ¢ âˆ€ (x âˆˆ Ï‰ â†’ (âˆ€ y ( y = S(x) â†’ y âˆˆ Ï‰))
    apply allI, -- x
    apply impI, -- assume `x âˆˆ Ï‰`
    apply allI, -- y
    apply impI, -- assume `y=S(x)`
    apply impE âˆ€'(#0 is_inductive' â†’' #1 âˆˆ' #0),
    { -- Ï‰ x y âŠ¢ âˆ€w (w is inductive â†’ y âˆˆ w)
      apply allI, -- w
      apply impI, -- assume `w is inductive`
      apply impE (#2 âˆˆ' #0),
      { -- Ï‰ x y w âŠ¢ x âˆˆ w
        apply impE (#2 âˆˆ' #3),
        { -- Ï‰ x y  âŠ¢ x âˆˆ Ï‰
          apply hypI, 
          simp only [set.image_insert_eq], 
          right, right, left, refl },
        { -- Ï‰ x y w âŠ¢ x âˆˆ Ï‰ â†’ x âˆˆ w
          apply allE' (#0 âˆˆ' #4 â†’' #0 âˆˆ' #1) #2,
          apply impE (#0 is_inductive'),
          { -- Ï‰ x y w âŠ¢ w is inductive 
            apply hypI1 },
          { -- Ï‰ x y w âŠ¢ (w is inductive) â†’ Ï‰ âŠ† w
            apply allE_var0,
            apply impE (âˆ€' ( #0 âˆˆ' #4 â†”' âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0))),
            { -- Ï‰ x y w âŠ¢ Ï‰ = Ï‰ = { x | âˆ€ w ( w is inductive â†’ x âˆˆ w) }
              apply hypI,
              simp only [set.image_insert_eq],
                right, right, right, left, refl },
            { -- Ï‰ x y w âŠ¢ Ï‰ = Ï‰ = { x | âˆ€ w ( w is inductive â†’ x âˆˆ w) } â†’ ((w is inductive) â†’ Ï‰ âŠ† w)
              apply allE' _ #3,
              apply weak zfc_ax,
              exact omega_subset_all_inductive,
              simp only [set.image_insert_eq, lift_zfc_ax],  
              assume y yh, simp[yh],
              unfold is_inductive, 
              refl, } }, 
            refl }, },
      { -- Ï‰ x y w âŠ¢ x âˆˆ w â†’ y âˆˆ w
        apply impI, -- assume `x âˆˆ w`
        apply impE (#1 is_successor_of' #2),
        { -- Ï‰ x y w âŠ¢ y = S(x)
          apply hypI, 
          simp only [set.image_insert_eq],
          right, right, left, 
          dsimp[is_successor_of], refl },
        { -- Ï‰ x y w âŠ¢ y = S(x) â†’ y âˆˆ w
          apply allE' _ #1,
          apply impE (#2 âˆˆ' #0),
          { -- Ï‰ x y w âŠ¢ x âˆˆ w
            apply hypI1, },
          { -- Ï‰ x y w âŠ¢ x âˆˆ w â†’ (âˆ€ y ( y = S(x) â†’ y âˆˆ w))
            apply allE' _ #2,
            apply andEâ‚‚, 
            apply hypI, 
            simp only[set.image_insert_eq],
            right, left, refl,
            unfold is_successor_of, refl },
        { unfold is_successor_of, 
          refl } } } },
    { -- Ï‰ x y âŠ¢ âˆ€w (w is inductive â†’ y âˆˆ w) â†’ y âˆˆ Ï‰
      apply iffE_l,
      apply allE_var0,
      apply hypI,
      simp only [set.image_insert_eq],
      right, right, left, refl } },
end

/--
  A formal proof that `Ï‰` is the smallest inductive set.

  Informally : 
  `zfc_ax âŠ¢ âˆ€ X ( A = Ï‰ â†’ (A is inductive âˆ§ âˆ€ w (w is inductive â†’ X âŠ† w))`
-/
def omega_smallest_inductive : 
  zfc_ax âŠ¢ âˆ€' ( âˆ€'( #0 âˆˆ' #1 â†”' âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)) 
                    â†’' ((#0 is_inductive') âˆ§' âˆ€'((#0 is_inductive') â†’' #1 'âŠ† #0))) :=
begin
  apply allI, -- Ï‰
  apply impI, -- Ï‰ = { x | âˆ€ w ( w is inductive â†’ x âˆˆ w) }
  apply andI,
  { -- Ï‰ âŠ¢ Ï‰ is inductive 
    apply impE_insert,
    apply allE_var0,
    simp only [lift_zfc_ax],
    apply omega_inductive
  },
  { -- Ï‰ âŠ¢ âˆ€ w (w is inductive â†’ Ï‰ âŠ† w)
    apply impE_insert,
    apply allE_var0,
    simp only [lift_zfc_ax],
    apply omega_subset_all_inductive },
end

end zfc_proofs

/--
  Main Theorem: ZFC proves that `Ï‰` is the smallest inductive set.
-/
theorem omega_smallest_inductive_provable_within_zfc :
 âˆ€' ( âˆ€'( #0 âˆˆ' #1 â†”' âˆ€' (#0 is_inductive' â†’' #1 âˆˆ' #0)) 
      â†’' ((#0 is_inductive') âˆ§' âˆ€'((#0 is_inductive') â†’' #1 'âŠ† #0))) is_provable_within zfc_ax :=
begin use omega_smallest_inductive, end

end zfc